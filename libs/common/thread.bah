#include "<pthread.h>"

#cLib "lpthread"

#define GC_pthread_create(id int*, attr ptr, func ptr, args ptr)
#define GC_pthread_join(id int, ret ptr)

#define! pthread_mutex_t int

#define pthread_mutex_init(lock pthread_mutex_t*, m int)
#define pthread_mutex_lock(lock pthread_mutex_t*)
#define pthread_mutex_unlock(lock pthread_mutex_t*)
#define pthread_mutex_destroy(lock pthread_mutex_t*)

//This structure implements a mutex.
//A mutex is a thread safe lock.
//*Note: the mutex needs to be initiated ans destroyed when you finished using it.*
struct mutex {
    id: pthread_mutex_t

    //This initiaties the mutex.
    init() {
        pthread_mutex_init(&this.id, 0)
    }

    //This locks the mutex.
    //Every other thread trying to lock it will need to wait for it to be unlocked.
    lock() {
        pthread_mutex_lock(&this.id)
    }

    //This unlocks the mutex.
    unlock() {
        pthread_mutex_unlock(&this.id)
    }

    //This destroyes the mutex.
    destroy() {
        pthread_mutex_destroy(&this.id)
    }

}

//A wrapper for a thread.
struct thread {
    id: int
    
    //This will need to ne set by the user.
    //The specified function will be the entry point for the thread.
    handle(id int) {

    }

    //This will launch the thread.
    create() {
        z0 = <ptr>0
        GC_pthread_create(&this.id, z0, this.handle, z0)   
    }

    //This will launch the thread by passing to its entry point funtion an argument.
    //The argument can only be a pointer.
    createWithArg(arg ptr) {
        z0 = <ptr>0
        GC_pthread_create(&this.id, z0, this.handle, arg)   
    }

    //This waits until the thread has finished executting.
    wait() {
        z0 = <ptr>0
        GC_pthread_join(this.id, z0)
    }
}

//This initiates a mutex and returns it.
mutex() mutex {
    m = mutex{}
    m.init()
    return m
}

//Example
// #include "iostream.bah"
// #include "thread.bah"
// printHello() {
// println("Hello")
// }
// main() {
// t = thread{}
// t.handle = &printHello
// t.create()
// t.wait()
// }
